Title: 使用Xtend扩展Java语言
Date: 2012-1-10


Java是这个星球最成功的语言。将近百万的开发者用它开发各种软件；地球上最屌爆的IDE都是Java的IDE；浩瀚的开源框架、类库、工具是让人们对它爱不释手。此外，Java的很多设计理念都是经过深思熟虑的。最重要的一点是，这些都是易于理解并且久经考验的。

但是（终于但是了）——

Java也是一门公认的死板的语言，让开发者一遍遍地重复冗余的代码。事实上这也是为什么Java的IDE为什么这么碉堡的原因：帮助我们去写那些冗余的代码。

同样众所周知的，阅读代码的时间比写下这段代码的时间要多。所以从这个角度来看Java确实存在很大的问题。或许可以通过IDE来改善这个问题，不过这就相当于又挖了一个依赖工具的大坑。解决这个问题要是要从根源上来：我们需要一个更好的Java。

<!--more-->
## Java + Xtend == Better Java
Xtend是Eclipse.org开发的静态类型语言。这门语言花了大量精力去平衡Java语言的精粹部分与无缝衔接现存的大量Java框架、类库。有经验的Java程序员花不了一会儿时间就能上手这门似曾相识的语言。Xtend的编辑器与Eclispe的JDT紧密集成，让你使用时不会有任何不习惯。

Java语言有大量的精粹部分，Xtend的主要工作就是扩展这些精粹。举几个例子，有了Xtend你就可以扔掉那些代码生成器了。默认访问权限，类型推导，还有其他几项有用的功能可以大大的增强代码可读性。并且Xtend支持闭包与操作符重载。使用Xtend你可以做到之前Java做不到的一些功能，并能免除新学习一门语言的痛苦。

Xtend只是作为一个插件来增强Java而不是取代它。写Xtend代码的时候你依然是在写熟悉的Java风格的接口，枚举以及注释。如果你哪天觉得不爽了依然可以毫无痛苦的回退回Java上。（译注：Xtend会编译成可读性良好的Java代码而不是字节码）

## Hello World!
Xtend写的“Hello World”:
<pre>class HelloWorld {
    def static void main(String[] args) {
        println("Hello World!")
    }
}</pre>
乍一看与Java最大的区别应该是用来声明方法的def关键词。还有就是没有强制分号分割符与没有显式的访问权限修饰符。Xtend中所有的类与方法的默认访问权限修饰都是public。与之不同的就是类中的字段，默认的都是private访问修饰符。Xtend支持与Java完全相同的private，protected，public权限修饰符。不过不支持Java的default权限修饰符（包可见）。

println函数会像java.lang一样自动的导入。这个函数在Xtend的一个简单的运行时类库中定义，这个类库只包括几个类来给Java SDK提供一些简洁的用法。

## 摆脱代码生成器
下面看一个更有实际意义的例子，把一个字符串列表的每个元素全部都转换成大写形式。这是用最冗余的形式写的:
<pre>def public List&lt;String&gt; toUpperCase(List&lt;String&gt; strings) {
    val List&lt;String&gt; result = new ArrayList&lt;String&gt;();
        for (String string : strings) {
            result.add(string.toUpperCase());
        }
    return result;
}</pre>
除了def和val关键词之外看起来和Java代码没什么区别，现在用类型推断和其他的一些法糖写一个更加简洁的:
<pre>def toUpperCase(List&lt;String&gt; strings) {
    val result = new ArrayList&lt;String&gt;()
        for (string : strings) {
            result += string.toUpperCase
        }
    return result
}</pre>
这样看上去就好很多了。因为编译器可以根据等号后面推断出变量的类型，所以我们移除了前面的类型声明。如果你觉得显式写出变量类型可以增强代码可读性的话也是可以的，总之在Xtend里这些都不是强求的。

如果细心的话你可能注意到了刚刚我们使用运算符+=来代替List.add(T)的方法调用了。在Xtend中运算符与方法调用是等价的，所以你可以在任何类型上定义自己的运算符。java.util.Collection上的+=运算符是在上面提到的运行时库中定义的运行时库。这项功能不只能让你在类似java.math.BigDecimal类型上使用漂亮的算数运算符，还可以用在很多其他需要的情况。比如说在Xtend中你可以这样创建HashMap:
<pre>val map = newHashMap( 1 -&gt; ‘one’, 2 -&gt; ‘two’, 3 -&gt; ‘three’ )</pre>
实际上-&gt;操作符是类似这样定义的:
<pre>def static &lt;A, B&gt; Pair&lt;A, B&gt; operator_mappedTo(A a, B b) {
    return new Pair&lt;A, B&gt;(a, b);
}</pre>
再次回到上面将字符串列表转换为大写的例子上。尽管我们应用的语法糖增强了代码的可读性，这段代码依然十分繁琐也容易出错。

## 进入函数式编程的世界
函数式编程倡导状态不可变，无副作用的编程风格，避免状态可变的编程风格。在严格的函数式编程中并没有状态一说，所有的东西都是函数表达式，在同样的输入参数都会得到相同的结果，没有状态这一说。

作为一种类Java语言，Xtend没有走得那么极端但也走了很远。举个例子，Xtend没有语句，所有看起来像是其实都是表达式，一个代码块也是表达式。你可以用Java中不允许的语法，比如这样:
<pre>val data = try {
    fileContentsToString("data.txt")
} catch (IOException e) {
    "dummy data"
}</pre>
Java里不允许使用try-cache语句来给变量复制，因为它是没有返回值的语句。在Xtend中，所有的东西都是表达式，所以得到的结果将回事fileContentsToString或者如果发生了IOException的话是"dummy data"。顺带一提，Xtend并不强迫你检查所有的异常。如果有异常产生的话编译器只是重新抛出了这个异常，这里用到的技术由Lombok(<a href="http://projectlombok.org/features/SneakyThrows.html">http://projectlombok.org/features/SneakyThrows.html</a>)引入。

函数编程的另一要素是高阶函数，此类函数以其他函数为参数。在Java中已经有了此类函数，比如java.util.Collections#sort(List, Comparator)就是一个高效的高阶函数，因为Comparator通常是一个实现了对同一类型的两个实力进行大小比较的函数。但在Java中没有简介的语法来实现一个Comparator的实例，最简介的方法是使用匿名类。

在Xtend中你可以使用函数变量来代替匿名内部类。这些函数变量更有可读性，也可以作为参数传递给高阶函数。这些函数可以自动被调用对象的类型。Xtend使用类似Smalltalk的语法来定义这些函数，也给Java的集合库添加了一些扩展的方法，可以让你像上一个例子里的一样修改List中的字符串:
<pre>def toUpperCase(List&lt;String&gt; strings) {
    strings.map( string | string.toUpperCase )
}</pre>
函数风格的编程不仅能增强代码可读性还大大减少了出错的几率。需要注意的是像其他现代语言一样，return不是必须的，返回值不是void的方法的最后一个表达式将作为返回值返回。

## 扩展方法
估计你会很想知道神奇的map方法是如何被绑定到java.util.List上的，其实它是在ListExtension中定义的，作为一个扩展方法被自动导入。这是Xtend运行时库的一部分，最终由Google Guava（http://code.google.com/p/guava-libraries/）来实现的(?)。如果你想自己定义扩展方法或者将现有的方法作为拓展方法使用，来看一个例子:
<pre>
import static extension java.util.Collections.*
class Test {
    def printSorted(List&lt;String&gt; strings) {
        // calls Collections.sort(List)
        println( strings.sort() )
        // calls Collections.sort(List,Comparator)
        println( strings.sort [ a, b | a.compareTo(b) ] )
    }
}
</pre>
例子中的两个sort调用都会调用到java.util.Collections的静态成员。第二个例子也展示了闭包如何自动转换为了Java现有类型。这种情况下闭包会被编译为匿名的Compatator类。

扩展方法在链式表达式中用起来更带感，因为你可以直接从左到右读和写代码就可以了。用典型的函数式中缀表达式语法写代码比前前后后的写法看上去要顺眼多了。假设有下面崎岖的函数调用:
<pre>writeToFile( escape(myText), "Greeting.txt")</pre>
追踪代码执行过程的时候你得前前后后的看代码。如果使用拓展方法的语法就可以这么写了:
<pre>myText.escape.writeToFile("Greeting.txt")</pre>
与其他语言不同，Xtend不强制要求拓展方法必须为static。任何局部声明的方法都可以作为拓展方法来用。如果你使用依赖注入的话这样会非常有用的，下面也会展示。不过你如何为一个完整的类层级定义多态的扩展方法呢(?)？

## 多重方法（aka动态分配）(?)
假如你有一个各种形状的类层级结构，Shape是Circle和Rectangle的抽象父类。将来你可能需要在这些形状类上做各种各样的计算，同时却不想每次增加新函数和子类的时候都修改和编译整个类层级。

其实这就是传说中的“the expression problem(<a href="http://c2.com/cgi/wiki?ExpressionProblem">http://c2.com/cgi/wiki?ExpressionProblem</a> )”，Xtend用十分优雅的方式解决了这个问题。

新的函数可以用拓展方法来定义。比如下面的例子，计算形状的面积可以这样:
<pre>
class ShapeGeometry {
    def getArea(Circle c) {
        PI * c.radius * c.radius
    }
    def getArea(Rectangle r) {
        r.length * r.width
    }
}
</pre>
使用扩展方法就可以计算出圆和矩形的面积了:
<pre>new Circle(5).area
new Rectangle(4, 5).area</pre>
不过我们更需要的是直接在父类Shape的实例上直接调用area，而且会依据具体子类型调用不同实现。可以在声明这些重载方法时加上dispatch函数来实现:
<pre>class ShapeGeometry {
    def dispatch area(Circle c) {
    PI * c.radius * c.radius
}

def dispatch area(Rectangle r) {
    r.length * r.width
    }
}</pre>
编译器会推断出一个函数签名相同的多态方法。这个例子中是def int getArea(Shape shape)，因为Circle和Rectangle的父类都是Shape。这个方法的实现会在运行时的类型信息将每次调用分配到正确的处理地方。

下面是如何在代码中使用上述的扩展方法:
<pre>@Inject
extension ShapeGeometry
def testArea() {
    val Shape shape = new Rectangle(4, 5)
    assertEquals( 20, shape.area )
}</pre>
正如你看到的那样这里没有使用静态方法来做扩展方法，但是扩展方法依然被注入了。我们更倾向于使用Google Guice（http://code.google.com/p/google-guice/），但是任何其他的依赖注入容易都可以做到，你也可以使用以前的工厂模式或者其他类似的东西去初始化字段(?)。

扩展字段不需要命名，因为不会直接引用到它们。shape.area表达式将会被翻译为this._shapeGeometry.area(shape)。

## 添加新的函数
假设你想给类层级增加一个Triangle类，但是却不想修改和重新编译原来的代码，这个时候一般你会怎么做？

在Xtend中你可以定义Triangle类，再创建一个ShapeGeometry的子类，将分配方法添加进去:
<pre>class ShapeGeometryForTriangles extends ShapeGeometry {
    def dispatch area(Triangle t) {
        (t.base * t.height) / 2
    }
}</pre>
Xtend就可以推断出一个新的用来合成的分配方法，重写了父类中的方法。调用会被分配到所有声明了dispatch的方法上，包括从父类集成的方法。现在你只需告知使用的依赖注入框架假如请求ShapeGeometry的时候注入ShapeGeometryForTrianles就可以了。

## DSL支持
领域专用语言（DSL）是指为了解决特定场景问题的可执行语言。假设你在使用一套Web开发框架，一项基本的任务就是将http请求映射到controller方法或者其他资源上去。相比没有类型检查看上去挺崎岖的XML文件，你可以这样做:
<pre>GET /index            -&gt; StartController.index()
GET /list/Persons     -&gt; PersonController.list()
GET /edit/Person/{id} -&gt; PersonController.edit(id)</pre>
在Xtend支持下的语法检查等等可以提示你错误的映射等等问题看上去难道不是很屌爆吗？类和方法的引用都是静态的，还支持跨语言重命名重构。在Xtend强大的DSL支持下这些都可以轻松做到。

Xtend构建在Xtext——Eclipse的开发语言和IDE框架框架上。在这些工具集的支持下你可以立刻创建这种请求映射DSL。你可以在任意地方嵌入Xtend的语法与Java的类型系统和注释。这些语言更具有函数性，IDE支持的也更好，比你之前用的那些DSL要好很多。你可以看一下Xtext官网上的教程（http://www.xtext.org/）来学习如何去做。

## 马上就试试吧！
不断的学习一门又一门完全不同的语言对开发者来说是家常便饭，将一个完整的项目以及项目成员迁移到新的语言确实项无比庞大的工程。Xtend能让你使用已有的Java知识和现存的Java代码。同时它简洁清新(= =)的语法和对函数式编程的鼓励会让编程充满乐趣。这项工具与Eclipse的JDT非常接近，在某些方面还更好用一些。而且Xtend的编译器完全不依赖于Eclispe，任何的Java进程都可以执行它。而且还提供了Maven插件来将Xtend代码编译为Java源代码。

如果你已经跃跃欲试了，你可以用Eclipse Market Place Client安装或者直接从官网（http://www.xtend-lang.org/ ）下载已经打包好的Eclipse发行版。接下来你可以尝试一下用向导创建一个tutorial工程，不过既然你已经看完了这篇文章基本可以立马写代码了。你可以在现有的Java工程中创建新的Xtend代码。提示一句，阅读单元测试一般都是好的上手点。;)

原文：<a href="http://pragprog.com/magazines/2011-12/extending-java">http://pragprog.com/magazines/2011-12/extending-java</a>

翻译：<a href="http://aisk.sinaapp.com/?p=80">http://aisk.sinaapp.com/?p=80</a>
